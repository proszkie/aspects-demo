plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

group = 'com.proszkie'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

configurations {
	runtimeAgent
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	compileOnly 'org.projectlombok:lombok:1.18.12'
	annotationProcessor 'org.projectlombok:lombok:1.18.12'

	runtimeAgent "org.aspectj:aspectjweaver"
	runtimeAgent "org.springframework:spring-instrument"

	testCompileOnly 'org.projectlombok:lombok:1.18.12'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

bootRun {
	doFirst {
		// Ensure that all of the agents we need to load at run-time happen for tests
		configurations.runtimeAgent.each {
			File jarFile ->
				jvmArgs "-javaagent:${jarFile.absolutePath}"
		}
	}
}

test {
	useJUnitPlatform()

	doFirst {
		// Ensure that all of the agents we need to load at run-time happen for tests
		configurations.runtimeAgent.each {
			File jarFile ->
				jvmArgs "-javaagent:${jarFile.absolutePath}"
		}
	}
}
